plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.cinnober.gradle.semver-git' version '3.0.0'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

pluginBundle {
    website = 'https://github.com/deepy/gradle-license-plugin'
    vcsUrl = 'https://github.com/deepy/gradle-license-plugin.git'
    tags = ['licensing', 'reporting']
}

gradlePlugin {
    plugins {
        licensing {
            id = 'com.github.deepy.licensing-report'
            implementationClass = 'com.github.deepy.licensing.GradleLicensePlugin'
            displayName = 'Gradle license usage report plugin'
            description = 'Creates reports of licenses used in your build.'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    dependsOn(tasks.functionalTest)
}
